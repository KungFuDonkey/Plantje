// DEBUG or RELEASE for logging
#define DEBUG

#define ARDUINO 10813

#define numToCharArray(x) String(x).c_str()
#define numToString(x) String(x)
#ifdef DEBUG
#define LOG(x) Serial.print(x)
#define LOGLN(x) Serial.println(x)
#define BEGINLOGGING Serial.begin(115200)
#define WAITONLOGGER while(!Serial)
#else
#define LOG(x)
#define LOGLN(x)
#define BEGINLOGGING
#define WAITONLOGGER
#endif

#include <ESP8266WiFi.h>
#include <Servo.h>
#include <Wire.h>
#include <Adafruit_BME280.h>
#include <Adafruit_SSD1306.h>
#include "WifiCredentials.h"
#include "PubSubClient.h"
#include "EventQueue.h"
#include "Encrypting.h"


#define D0 16
#define D1 5
#define D2 4
#define D3 0
#define D4 2
#define D5 14
#define D6 12
#define D7 13
#define D8 15


#define SERVO_PIN D5
#define ONBOARD_LED 16    // LED on the side of the processor
#define ESP8266_LED 2     // LED on the side of the wifi unit
#define AMUX_PIN A0
#define AMUX_SELECT D6

//publish topics
#define TEMPERATURETOPIC GENERALTOPIC "home/bedroom/temperature\0"
#define WILLTOPIC GENERALTOPIC "home/bedroom/esp/espConnected\0"
#define MOISTURETOPIC GENERALTOPIC "home/bedroom/moisture\0"
#define PRESSURETOPIC GENERALTOPIC "home/bedroom/airpressure\0"
#define HUMIDITYTOPIC GENERALTOPIC "home/bedroom/humidity\0"
#define LIGHTTOPIC GENERALTOPIC "home/bedroom/light\0"
#define MODETOPIC GENERALTOPIC "home/bedroom/esp/mode\0"


//subscribe topics
#define BUTTONTOPIC GENERALTOPIC "home/bedroom/button\0"
#define GESTURETOPIC GENERALTOPIC "home/bedroom/gestureDevice\0"

//Network
WiFiClient espClient;
PubSubClient client(espClient);
#define MSG_BUFFER_SIZE	(50)
char msg[MSG_BUFFER_SIZE];

//Servo
Servo servo;
int lastServoTime = 0;
int servoDelay = 3000;
bool servoUp = false;

//Real OLED display
#define OLED_RESET     -1
#define SCREEN_ADDRESS 0x3c
Adafruit_SSD1306 display(128, 64, &Wire, OLED_RESET);

//Queue
EventQueue queue;

//BME280
Adafruit_BME280 bme;

//Temperature sensor
float temp = 0;
float prevtemp = 0;

//Light sensor
float light = 0;
float prevlight = 0;

//Moisture sensor
float moisture = 0;
float prevmoisture = 0;
bool moistureRead = false;

//Humidity sensor
float humidity = 0;
float prevhumidity = 0;

//Airpressure sensor
float pressure = 0;
float prevpressure = 0;

//Watering the plant
#define WaterTime 3000
#define CancelWaterTime 10000
unsigned long lastWateredTime = 0;

//Mode
bool manual = false;

//Menus
int menu = 0;
#define menuAmount 3
#define AutoMenuSwitchTimer 4000
int plantStatus = 0; // 0 = sad; 1 = happy

//Flash button
int prevButton = LOW;
int button = LOW;

//Encyptor
Encryptor encryptor;

void setup() 
{
  pinMode(ONBOARD_LED, OUTPUT);
  pinMode(ESP8266_LED, OUTPUT);
  pinMode(AMUX_SELECT, OUTPUT);
  pinMode(AMUX_PIN, INPUT);
  pinMode(2, OUTPUT);
  pinMode(0, INPUT_PULLUP);
  servo.attach(SERVO_PIN);
  BEGINLOGGING;
  WAITONLOGGER;
  
  // Setup BME
  if (!bme.begin(0x76))
  {
    LOGLN("Could not find a valid BME280 sensor");
    while (1);
  }

  // Setup display
  if(!display.begin(SSD1306_SWITCHCAPVCC, SCREEN_ADDRESS))
  {
    LOGLN("SSD1306 allocation failed");
    while (1);
  }
  display.display();

  setup_wifi();

  client.setServer(SERVER,SERVERPORT);
  client.setCallback(callback);

  servo.write(135);
  EnqueueSensors();
  queue.Enqueue(AutoMenuUp,AutoMenuSwitchTimer);
  PublishMode();
}

void EnqueueSensors(){
  queue.Enqueue(UpdateTemp, 1000);
  queue.Enqueue(UpdateHumidity, 1000);
  queue.Enqueue(UpdateLight, 1000);
  queue.Enqueue(UpdateMoisture, 1000);
  queue.Enqueue(UpdatePressure, 1000);
}

void setup_wifi(){
  WiFi.mode(WIFI_STA);
  WiFi.begin(SSID,PASSWORD);
  int i = 0;
  while(WiFi.status() != WL_CONNECTED){
    delay(1000);
    LOG(++i); 
    LOG(' ');
  }
  LOGLN();
  LOG("Connection established: ");
  LOGLN(SSID);
  LOG("Ip address: ");
  LOGLN(WiFi.localIP());
}

void loop() 
{
  MQTT_checkConnection();
  
  queue.PerformEvents();
  Menu(menu);

  button = digitalRead(0);
  if(button != prevButton && button == LOW){
    SwitchMode();
  }
  prevButton = button;
}

//--------------------------------------------------------------------------------------------------------------------------------------------------------------
// MQTT networking

void MQTT_checkConnection(){
  if(!client.connected()){
    reconnect();
  }
  client.loop();
}

void reconnect(){
  while (!client.connected()) {
    LOG("Attempting MQTT connection...");
    String clientId = "ESP8266Client-";
    clientId += String(random(0xffff), HEX);
    if (client.connect(clientId.c_str(),USERNAME,KEY,WILLTOPIC,1,true,"DISCONNECTED")) {
      LOGLN("connected");
      client.publish(WILLTOPIC,"CONNECTED",true);
      //subsciptions here
      client.subscribe(BUTTONTOPIC);
      client.subscribe(GESTURETOPIC);
      break;
    } 
    LOG("failed, rc=");
    LOG(client.state());
    LOGLN(" try again in 5 seconds");
    delay(5000);
  }
}

void callback(char* topic, byte* payload, unsigned int length){
  LOG("Message arrived [");
  LOG(topic);
  LOG("] ");
  char* msg = new char[length + 1];
  for(int i = 0; i < length; i++){
    LOG((char)payload[i]);
    msg[i] = (char)payload[i];
  }
  msg[length] = '\0';
  LOGLN();
  char* message = encryptor.Decrypt(msg,topic,String(topic).length());
  LOGLN(message);
  String check = String(topic);
  if(check.equals(BUTTONTOPIC)){
    ButtonAction(message);
  }
  if(check.equals(GESTURETOPIC)){
    GestureAction(message);
  }
}

//---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
//Encryption

bool publishMessage(const char *topic, String payload){
  return client.publish(topic,encryptor.Encrypt(payload.c_str(),payload.length(), topic, String(topic).length()),true);
}



//---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Gestures

void GestureAction(String msg){
  if(manual && msg[0] == 'a'){
    WaterPlant();
  }
  else if(msg[0] == 'b'){
    SwitchMode();
  }
  else if(manual && msg[0] == 'c'){
    MenuUp();
  }
  else if(manual && msg[0] == 'd'){
    ReloadVariables();
  }
}

void SwitchMode(){
  LOGLN("Switch mode");
  manual = !manual;
  LOGLN(manual);
  digitalWrite(2,!manual);
  PublishMode();
}

void PublishMode(){
  if(manual){
    publishMessage(MODETOPIC,String(manual));
    return;
  }
  publishMessage(MODETOPIC,String(manual));
  
}

void ReloadVariables(){
  temp = bme.readTemperature();
  pressure = bme.readPressure();
  humidity = bme.readHumidity();
  digitalWrite(AMUX_SELECT, LOW);
  light = analogRead(AMUX_PIN);
  moistureRead = true;
  digitalWrite(AMUX_SELECT, HIGH);
  queue.Enqueue(ReadMoisture, 100);
  queue.Enqueue(SetMenuToMax, 101);
}

//-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Online Button

void ButtonAction(String msg){
  if(msg == "1"){
    WaterPlant();
  }
}

//----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Temperature Sensor

void UpdateTemp(){
  temp = bme.readTemperature();
  MQTT_publishTemp();
  queue.Enqueue(UpdateTemp, 5000);
}

void MQTT_publishTemp(){
  if(publishMessage(TEMPERATURETOPIC,String(temp))){
    LOG("Published Temp: ");
    LOGLN(temp);
    prevtemp = temp;
  }
}

//------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Moisture Sensor

void UpdateMoisture(){
  moistureRead = true;
  digitalWrite(AMUX_SELECT, HIGH);
  queue.Enqueue(ReadMoisture, 100);
  queue.Enqueue(UpdateMoisture, 5000);
}

void ReadMoisture(){
  moisture = analogRead(AMUX_PIN);
  digitalWrite(AMUX_SELECT, LOW);
  if (moisture < 200 && prevmoisture < 200)
  {
    plantStatus = 0;
    if (!manual)
    {
      WaterPlant();
    }
  }
  if (moisture >= 200 && prevmoisture >= 200)
  {
    plantStatus = 1;
  }
  
  MQTT_publishMoisture();
  moistureRead = false;
}

void MQTT_publishMoisture(){
  if(publishMessage(MOISTURETOPIC,String(moisture))){
    LOG("Published Moisture: ");
    LOGLN(moisture);
    prevmoisture = moisture;
  }
}

//----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Light Sensor

void UpdateLight(){
  if (!moistureRead)
  {
    light = analogRead(AMUX_PIN);
    MQTT_publishLight();
    queue.Enqueue(UpdateLight, 2000);
    return;
  }
  queue.Enqueue(UpdateLight, 100);
}

void MQTT_publishLight(){
  if(publishMessage(LIGHTTOPIC,String(light))){
    LOG("Published Light: ");
    LOGLN(light);
    prevlight = light;
  }
}

//----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Humidity Sensor

void UpdateHumidity(){
  humidity = bme.readHumidity();
  MQTT_publishHumidity();
  queue.Enqueue(UpdateHumidity, 5000);
}

void MQTT_publishHumidity(){
  if(publishMessage(HUMIDITYTOPIC,String(humidity))){
    LOG("Published Humidity: ");
    LOGLN(humidity);
    prevhumidity = humidity;
  }
}

//------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
// Airpressure Sensor

void UpdatePressure(){
  pressure = bme.readPressure();
  MQTT_publishPressure();
  queue.Enqueue(UpdatePressure, 5000);
}

void MQTT_publishPressure(){
  if(publishMessage(PRESSURETOPIC,String(pressure))){
    LOG("Published pressure: ");
    LOGLN(pressure);
    prevpressure = pressure;
  }
}

void AutoMenuUp(){
  if(!manual){
    MenuUp();
  }
  queue.Enqueue(AutoMenuUp, AutoMenuSwitchTimer);
}

void MenuUp(){
  menu = menu + 1 == menuAmount ? 0 : menu + 1;
}

// Used for the ShowVariables method, it won't go to this screen automatically
void SetMenuToMax(){
  menu = menuAmount;
}

void Menu(int menu){
  display.clearDisplay();
  display.setTextSize(1);
  display.setTextColor(SSD1306_WHITE);
  display.setCursor(0,0);
  if(menu == 0)
  {
    ShowTempLight();
  }
  else if (menu == 1)
  {
    ShowHumPres();
  }
  else if (menu == 2)
  {
    ShowMoisture();
  }
  else if (menu == menuAmount)
  {
    ShowVariables();
  }
  display.display();
}

void ShowVariables(){
  display.print("Temp: ");
  float _temp = floor(temp * 10) / 10;
  display.println(_temp);
  display.print("Light: ");
  float _light = floor(light * 10) / 10;
  display.println(_light);
  display.print("Humidity: ");
  float _humidity = floor(humidity * 10) / 10;
  display.println(_humidity);
  display.print("Pressure: ");
  float _pressure = floor(pressure * 10) / 10;
  display.println(_pressure);
  display.print("Moisture: ");
  float _moisture = floor(moisture * 10) / 10;
  display.println(_moisture);
}

void ShowTempLight(){
  display.print("Temp: ");
  float _temp = floor(temp * 10) / 10;
  display.println(_temp);
  display.print("Light: ");
  float _light = floor(light * 10) / 10;
  display.println(_light);
}

void ShowHumPres(){
  display.print("Humidity: ");
  float _humidity = floor(humidity * 10) / 10;
  display.println(_humidity);
  display.print("Pressure: ");
  float _pressure = floor(pressure * 10) / 10;
  display.println(_pressure);
}

void ShowMoisture(){
  display.print("Moisture: ");
  float _moisture = floor(moisture * 10) / 10;
  display.println(_moisture);
  ShowSmiley();
}

const unsigned char sad_Pepe [] PROGMEM = {
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xff, 0x80, 0x1f, 0xe3, 0xff, 
  0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xfe, 0x7f, 0xe7, 0x00, 0x7f, 0xff, 0xff, 0xff, 
  0xf0, 0xff, 0xff, 0xff, 0xff, 0xfd, 0xff, 0xf0, 0xff, 0x9f, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 
  0xff, 0xff, 0xf9, 0xff, 0xf9, 0xff, 0xef, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xf3, 
  0xff, 0xfd, 0xff, 0xef, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xe7, 0xf0, 0x1d, 0xff, 
  0xf7, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xef, 0x9f, 0xf2, 0xff, 0xf7, 0xff, 0xff, 
  0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xcf, 0x7f, 0xfc, 0x03, 0x03, 0xff, 0xff, 0xff, 0xf0, 0xff, 
  0xff, 0xff, 0xff, 0xdf, 0xff, 0xfe, 0x7f, 0xfc, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 
  0xbf, 0xff, 0xff, 0x3f, 0xe1, 0x7f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xbf, 0xfe, 0x0f, 
  0x98, 0xff, 0x1f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xbf, 0xf1, 0xf0, 0x66, 0x00, 0x2f, 
  0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xfc, 0x3f, 0xce, 0x3f, 0xc1, 0xff, 0xc7, 0xff, 0xff, 0xf0, 
  0xff, 0xff, 0xff, 0xf9, 0x7f, 0x81, 0xfc, 0x06, 0x00, 0x03, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 
  0xf3, 0x7f, 0x0f, 0xc1, 0xf0, 0xf8, 0xf3, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xf3, 0x7e, 0xf0, 
  0x20, 0xf7, 0xe8, 0x7b, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xf7, 0x7f, 0x07, 0xd8, 0x77, 0xc2, 
  0x3b, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xe7, 0xff, 0xe7, 0x83, 0x3f, 0xc3, 0x37, 0xff, 0xff, 
  0xf0, 0xff, 0xff, 0xff, 0xef, 0xff, 0xf9, 0x80, 0x03, 0xc0, 0x07, 0xff, 0xff, 0xf0, 0xff, 0xff, 
  0xff, 0xcf, 0xff, 0xe6, 0x00, 0x66, 0x01, 0xcf, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xdf, 0xff, 
  0xf8, 0xff, 0x8f, 0xff, 0xbf, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0x9f, 0xff, 0xff, 0xc0, 0x7f, 
  0xff, 0x3f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xbf, 0xff, 0xff, 0xfe, 0x79, 0xfc, 0xff, 0xff, 
  0xff, 0xf0, 0xff, 0xff, 0xff, 0x3f, 0xff, 0xff, 0xfb, 0xfc, 0x00, 0xff, 0xff, 0xff, 0xf0, 0xff, 
  0xff, 0xff, 0x7f, 0xff, 0xff, 0xe7, 0xff, 0x0e, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfe, 0x7f, 
  0xff, 0xff, 0xbf, 0xff, 0xff, 0x7f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfe, 0xff, 0xff, 0xff, 0xff, 
  0xff, 0xff, 0x7f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfe, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xbf, 
  0xff, 0xff, 0xf0, 0xff, 0xff, 0xfc, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xbf, 0xff, 0xff, 0xf0, 
  0xff, 0xff, 0xfc, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xbf, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfc, 
  0xff, 0xff, 0x80, 0x3f, 0xff, 0xff, 0x9f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfc, 0xff, 0xff, 0x3f, 
  0xe1, 0xff, 0xfc, 0x1f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfe, 0xff, 0xfe, 0xff, 0xff, 0x00, 0x00, 
  0xdf, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfe, 0x7f, 0xfe, 0xd0, 0x07, 0xff, 0xff, 0xbf, 0xff, 0xff, 
  0xf0, 0xff, 0xff, 0xfe, 0x7f, 0xfe, 0xff, 0xf8, 0x1e, 0x00, 0x7f, 0xff, 0xff, 0xf0, 0xff, 0xff, 
  0xff, 0x3f, 0xfe, 0x1f, 0xff, 0xc0, 0x0f, 0x7f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0x3f, 0xfb, 
  0x80, 0x3f, 0xff, 0xff, 0x7f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0x9f, 0xfb, 0xff, 0x00, 0x1f, 
  0xfe, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xcf, 0xfc, 0x7f, 0xff, 0xf0, 0x07, 0xff, 0xff, 
  0xff, 0xf0, 0xff, 0xff, 0xff, 0xe3, 0xff, 0xff, 0xff, 0xff, 0xcf, 0xff, 0xff, 0xff, 0xf0, 0xff, 
  0xff, 0xff, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0xbf, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 
  0x03, 0xff, 0xff, 0xfe, 0x7f, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xe0, 0x0f, 0xff, 
  0xe1, 0xff, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xff, 0xc0, 0x00, 0x0f, 0xff, 0xff, 
  0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf0, 
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf0
};

const unsigned char happy_Pepe [] PROGMEM = {
  0xff, 0xff, 0xff, 0xff, 0xff, 0x8f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 
  0xff, 0xfc, 0x20, 0x7f, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xf1, 0xff, 
  0x0f, 0x00, 0x3f, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0xc7, 0xff, 0xc4, 0x7f, 0xcf, 
  0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xff, 0x9f, 0xff, 0xfb, 0xff, 0xef, 0xff, 0xff, 0xff, 
  0xf0, 0xff, 0xff, 0xff, 0xff, 0xbf, 0xff, 0xfb, 0xff, 0xf7, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 
  0xff, 0xff, 0x7f, 0xf9, 0xfd, 0xff, 0xfb, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xfe, 0x7f, 
  0x8e, 0x1d, 0xff, 0xfb, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xfe, 0xfc, 0xff, 0xe5, 0xf8, 
  0x0b, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xfc, 0xfb, 0xff, 0xf8, 0x07, 0xf0, 0xff, 0xff, 
  0xff, 0xf0, 0xff, 0xff, 0xff, 0xfd, 0xff, 0xff, 0xfe, 0xff, 0xff, 0x3f, 0xff, 0xff, 0xf0, 0xff, 
  0xff, 0xff, 0xf9, 0xff, 0xfe, 0x00, 0x7f, 0xff, 0xdf, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xf9, 
  0xff, 0xf8, 0x7f, 0x3c, 0x00, 0x0f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xf3, 0xff, 0xe0, 0x78, 
  0xc7, 0x80, 0x37, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xc3, 0xff, 0x82, 0x03, 0x08, 0x7f, 0x83, 
  0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0x93, 0xfe, 0x00, 0x0f, 0x3f, 0x00, 0x19, 0xff, 0xff, 0xf0, 
  0xff, 0xff, 0xff, 0x37, 0xfe, 0x0c, 0x07, 0xed, 0x80, 0xe5, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfe, 
  0x77, 0xf0, 0x9d, 0x13, 0xf3, 0x82, 0xf8, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfe, 0xf7, 0xf3, 0x78, 
  0x13, 0xf7, 0x82, 0x7c, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfc, 0xf7, 0xfc, 0x78, 0x03, 0xef, 0x80, 
  0xfd, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfd, 0xff, 0xff, 0xcc, 0x07, 0xe7, 0xc0, 0xf1, 0xff, 0xff, 
  0xf0, 0xff, 0xff, 0xfd, 0xff, 0xff, 0xf0, 0x0f, 0x00, 0x40, 0x0b, 0xff, 0xff, 0xf0, 0xff, 0xff, 
  0xf9, 0xff, 0xff, 0xbc, 0x00, 0xef, 0x00, 0xf7, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf9, 0xff, 0xff, 
  0xe0, 0xff, 0x8f, 0xff, 0xef, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfb, 0xff, 0xff, 0xfe, 0x00, 0x3f, 
  0xff, 0xdf, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xfb, 0xff, 0xff, 0xff, 0xfc, 0xfb, 0xff, 0x3f, 0xff, 
  0xff, 0xf0, 0xff, 0xff, 0xfb, 0xff, 0xff, 0xff, 0xf1, 0xf8, 0x7c, 0x7f, 0xff, 0xff, 0xf0, 0xff, 
  0xff, 0xfb, 0xff, 0xff, 0xff, 0x8f, 0xfe, 0x00, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf3, 0xff, 
  0xff, 0xff, 0x7f, 0xff, 0x3e, 0x7f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf3, 0xff, 0xff, 0xff, 0xff, 
  0xff, 0x9f, 0x3f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf3, 0xff, 0xf8, 0x7f, 0xff, 0xff, 0xff, 0x9f, 
  0xff, 0xff, 0xf0, 0xff, 0xff, 0xf3, 0xff, 0xf0, 0x1f, 0xff, 0xff, 0xff, 0xdf, 0xff, 0xff, 0xf0, 
  0xff, 0xff, 0xf3, 0xff, 0xf7, 0xc7, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf3, 
  0xff, 0xf7, 0xf0, 0xff, 0xff, 0xff, 0xef, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf3, 0xff, 0xf6, 0x7c, 
  0x1f, 0xff, 0xff, 0xcf, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf3, 0xff, 0xf7, 0x1f, 0x80, 0xff, 0xfc, 
  0x07, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf3, 0xff, 0xfb, 0x87, 0xf0, 0x00, 0x01, 0xe7, 0xff, 0xff, 
  0xf0, 0xff, 0xff, 0xf3, 0xff, 0xf9, 0xf0, 0x7f, 0xff, 0xff, 0xcf, 0xff, 0xff, 0xf0, 0xff, 0xff, 
  0xfb, 0xff, 0xfc, 0xfc, 0x03, 0xff, 0xff, 0x9f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf9, 0xff, 0xff, 
  0x1f, 0xf0, 0x00, 0x00, 0x1f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf9, 0xff, 0xff, 0xe1, 0xff, 0xff, 
  0xff, 0x9f, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xf9, 0xff, 0xff, 0xfc, 0x1f, 0xff, 0xff, 0xbf, 0xff, 
  0xff, 0xf0, 0xff, 0xff, 0xfc, 0xff, 0xff, 0xff, 0xe0, 0x00, 0x7e, 0x3f, 0xff, 0xff, 0xf0, 0xff, 
  0xff, 0xfc, 0x3f, 0xff, 0xff, 0xff, 0xc0, 0x00, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0x0f, 
  0xff, 0xff, 0xff, 0xff, 0xe7, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0x83, 0xff, 0xff, 0xff, 
  0xff, 0xef, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xf0, 0x3f, 0xff, 0xff, 0xff, 0x9f, 0xff, 
  0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 0xfe, 0x01, 0xff, 0xff, 0xfc, 0x7f, 0xff, 0xff, 0xff, 0xf0, 
  0xff, 0xff, 0xff, 0xff, 0xf0, 0x01, 0xff, 0x01, 0xff, 0xff, 0xff, 0xff, 0xf0, 0xff, 0xff, 0xff, 
  0xff, 0xff, 0xf0, 0x00, 0x3f, 0xff, 0xff, 0xff, 0xff, 0xf0
};

void ShowSmiley(){
  if (plantStatus == 0)
  {
    display.drawBitmap(0, 10, sad_Pepe, 100, 50, WHITE);
    return;
  }
  display.drawBitmap(0, 10, happy_Pepe, 100, 50, WHITE);
}

void WaterPlant(){
  if (millis() - lastWateredTime > CancelWaterTime)
  {
    servo.write(45);
    queue.Enqueue(StopWateringPlant,3000);
    lastWateredTime = millis();
  }
}
void StopWateringPlant(){
  servo.write(135);
}
